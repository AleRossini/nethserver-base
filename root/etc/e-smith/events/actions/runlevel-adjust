#!/usr/bin/perl

#
# NethServer
#
# Copyright (C) 2012 Nethesis srl
#

use strict;
use esmith::ConfigDB;
use NethServer::Service;

my $event = shift || die("Missing event argument!");
my $confDb = esmith::ConfigDB->open_ro();
my $errors = 0;

sub chkconfig
{
    my $runlevels = shift;
    my $service = shift;
    my $action = shift;

    if(system('/sbin/chkconfig', '--level', $runlevels, $service, $action) != 0) {
	warn "runlevel-adjust: " . $! . "\n";
	$errors ++;
	return 0; # FAILURE
    }

    return 1; # OK
}

foreach my $serviceRecord ($confDb->get_all_by_prop('type' => 'service')) {

    my $serviceName = $serviceRecord->key;
    my $runLevels = $serviceRecord->prop('Runlevels');

    if( ! defined($runLevels)) {
	next;
    }

    $runLevels = join('', split(',', $runLevels));

    if( ! $runLevels =~ m/\d+/ ) {
	warn "Invalid runlevel specification on service $serviceName: $runLevels";
	$errors ++;
	next;
    }

    # Start the service if is enabled in current runlevel
    if(NethServer::Service::is_enabled($serviceName)) {
	chkconfig($runLevels, $serviceName, 'on') != 0 || next;		
	if( ! NethServer::Service::start($serviceName)) {
	    warn "runlevel-adjust: error while starting `$serviceName`\n";
	    $errors ++;
	}
    } else {
	chkconfig($runLevels, $serviceName, 'off') != 0 || next;
	NethServer::Service::stop($serviceName);
    }

}

exit($errors > 0 ? 1 : 0);
